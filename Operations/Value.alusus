/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus Rows library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module Rows {
    class Value {
        def Type: {
            def NULL: 0;
            def INT: 1;
            def FLOAT: 2;
            def STRING: 3;
            def BOOLEAN: 4;
        }

        def valueType: Int;
        def intValue: Int[64];
        def floatValue: Float[64];
        def strValue: String;

        handler this~init() {
            this.valueType = Type.NULL;
        }
        handler this~init(Bool) {
            this.valueType = Type.BOOLEAN;
            this.intValue = value;
        }
        handler this~init(Nullable[Bool]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.BOOLEAN;
                this.intValue = value;
            }
        }
        handler this~init(Int[64]) {
            this.valueType = Type.INT;
            this.intValue = value;
        }
        handler this~init(Nullable[Int[32]]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.INT;
                this.intValue = value;
            }
        }
        handler this~init(Nullable[Int[64]]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.INT;
                this.intValue = value;
            }
        }
        handler this~init(Float[64]) {
            this.valueType = Type.FLOAT;
            this.floatValue = value;
        }
        handler this~init(Nullable[Float[32]]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.FLOAT;
                this.floatValue = value;
            }
        }
        handler this~init(Nullable[Float[64]]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.FLOAT;
                this.floatValue = value;
            }
        }
        handler this~init(String) {
            this.valueType = Type.STRING;
            this.strValue = value;
        }
        handler this~init(Nullable[String]) {
            if value.isNull this.valueType = Type.NULL
            else {
                this.valueType = Type.STRING;
                this.strValue = value;
            }
        }
        handler this~init(CharsPtr) {
            if value == 0 this.valueType = Type.NULL
            else {
                this.valueType = Type.STRING;
                this.strValue = value;
            }
        }

        handler this~init(ref[this_type]) {
            this = value;
        }
        handler this = ref[this_type] {
            this.valueType = value.valueType;
            this.intValue = value.intValue;
            this.floatValue = value.floatValue;
            this.strValue = value.strValue;
        }

        handler this.toString(): String {
            if this.valueType == Type.NULL return String("null")
            else if this.valueType == Type.INT return String() + this.intValue
            else if this.valueType == Type.FLOAT return String() + this.floatValue
            else if this.valueType == Type.BOOLEAN return String("'") + this.intValue + "'"
            else return String("'") + escapeString(this.strValue) + "'";
        }
    }
}
