/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus Rows library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module Rows {
    class Insert {
        def _table: String;
        handler this.table = String {
            this._table = escapeString(value);
            return this._table;
        }

        def _data: Array[Value];
        handler this.data = Array[Value] {
            this._data.clear();
            def i: Int;
            for i = 0, i < value.getLength(), ++i {
                this._data.add(value(i));
            }
            return this._data;
        }

        def _columns: Array[String];
        handler this.columns = Array[String] {
            this._columns.clear();
            def i: Int;
            for i = 0, i < value.getLength(), ++i {
                this._columns.add(escapeString(value(i)));
            }
            return this._columns;
        }

        handler this.generateColsString(): String {
            def str: String = "(";

            def size: int = 0;
            if this._data.getLength() < this._columns.getLength() size = this._data.getLength()
            else size = this._columns.getLength();

            def i: int = 0;
            for i = 0, i < size - 1, ++i {
                str += String("\"") + this._columns(i) + "\", ";
            }
            str += String("\"") + this._columns(size-1) + "\")";

            return str;
        }

        handler this.generateValsString(): String {
            def str: String = "(";

            def size: int = 0;
            if this._data.getLength() < this._columns.getLength() size = this._data.getLength()
            else size = this._columns.getLength();

            def i: int = 0;
            for i = 0, i < size - 1, ++i {
                str += this._data(i).toString() + ", ";
            }
            str += this._data(size-1).toString() + ")";

            return str;
        }

        handler this.generateSqlString(): String {
            return String("INSERT INTO ") + this._table + this.generateColsString() + "\n"
                + "VALUES " + this.generateValsString();
        }
    }
}

