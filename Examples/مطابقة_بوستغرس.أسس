اشمل "مـتم/نـص"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/بـعدم"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/بـوستغرس.أسس" })؛
استخدم مـتم؛
استخدم صـفوف؛

@جدول["سيارات"، 1]
صنف سـيارة {
    عرف_أساسيات_الجدول[]؛

    @إلزامي
    @فهرس_رئيسي
    @عـدد_صحيح
    @حقل
    عرف المعرف: صحيح؛

    @مـحارف_مرنة["50"]
    @مبدئي["سيارتي"]
    @حقل
    عرف الاسم: نـص؛

    @عـدد_عائم
    @حقل["سعر_السيارة"]
    عرف السعر: عائم؛

    @ثـنائية
    @حقل
    عرف كهربائية: ثـنائي؛

    @تـاريخ_ووقت
    @حقل
    عرف تاريخ_الإنشاء: بـعدم[نـص]؛
}

عرف قب: قـاعدة_بيانات(مـشغل_بوستغرس(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
    المنفذ = 5432؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1، نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

قب.مهيكل[سـيارة].واكب()؛

قب.من[سـيارة].احذف()؛

عرف ع: صحيح=1
لكل ع = 1، ع < 30، ع = ع+1 {
    عرف س: سـيارة؛
    س.المعرف = ع
    س.الاسم = نـص("سيارة ") + ع؛
    س.السعر = 200.0 * ع
    س.كهربائية = ع > 15؛
    إذا ع < 2 س.تاريخ_الإنشاء = "2023-02-18 10:57:33"؛
    قب.احفظ[سـيارة](س)؛
}
طـرفية.اطبع("\ج")؛

دالة اطبع_حقولا(ح: مـصفوفة[سـندنا[سـيارة]]) {
    عرف ك: صحيح = 0؛
    لكل ك = 0، ك < ح.هات_الطول()، ك = ك+1 {
        طـرفية.اطبع(ح(ك).المعرف)؛
        طـرفية.اطبع("\t\t")؛
        طـرفية.اطبع(ح(ك).الاسم)؛
        طـرفية.اطبع("\t\t")؛
        طـرفية.اطبع(ح(ك).السعر)؛
        طـرفية.اطبع("\t\t")؛
        إذا ح(ك).كهربائية طـرفية.اطبع("كهربائية") وإلا طـرفية.اطبع("تقليدية")؛
        إذا ليس ح(ك).تاريخ_الإنشاء.أهو_عدم {
            طـرفية.اطبع("\t\t")؛
            طـرفية.اطبع(ح(ك).تاريخ_الإنشاء)؛
        }
        طـرفية.اطبع("\ج")؛
    }
}

عرف د: مـصفوفة[سـندنا[سـيارة]]؛
د = قب.من[سـيارة].حيثما[(السعر < 1000 أو السعر > 5000) و المعرف > 2].رتب[المعرف].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د(0).احذف()؛

د(1).الاسم = نـص("سيارة 4م")؛
د(1).السعر = 400؛
د(1).احفظ()؛

قب.من[سـيارة].حيثما[المعرف == 20].حدث[{ السعر=90، الاسم="BMW" }]؛

قب.من[سـيارة].حيثما[المعرف > 23 و المعرف <= 28].احذف()؛

د = قب.من[سـيارة].رتب[المعرف].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[ليس (المعرف > 10)].رتب[المعرف].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[!(المعرف في (1، 2، 5، 7))].رتب[المعرف].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[المعرف في (1, 2, 4, 5, 7)].رتب[(-السعر, المعرف)].اجلب();
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[المعرف في (1, 2, 4, 5, 7)].رتب[(-السعر, -المعرف)].اجلب();
اطبع_حقولا(د)؛

