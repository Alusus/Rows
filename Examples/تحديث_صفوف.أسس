اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/ريـاضيات"؛
اشمل "مـتم/وقـت"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، "صـفوف.أسس")؛
استخدم مـتم؛
استخدم صـفوف؛

عرف قب: قـاعدة_بيانات(سـائق_مايسكويل(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1, نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

ريـاضيات.جهز_العشوائية(وقـت.هات_الختم_الزمني(0))؛
عرف الاسم_الجديد: نـص = نـص("سرمد ") + (ريـاضيات.عشوائي() % 20)؛
عرف العنوان_الجديد: نـص = نـص() + (ريـاضيات.عشوائي() % 1000) + ", سَري"؛

إذا !قب.نفذ(تـحديث().{
    الجدول = نـص("users")؛
    الحقول = مـصفوفة[نـص]({ نـص("name"), نـص("address") })؛
    البيانات = مـصفوفة[نـص]({ الاسم_الجديد, العنوان_الجديد })؛
    الشرط = نـص("id=150")؛
}) {
    نـظام.فشل(1, نـص("فشلت العملية: ") + قب.هات_آخر_خطأ())؛
}

