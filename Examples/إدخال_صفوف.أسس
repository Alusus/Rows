اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/مـايسكويل.أسس" })؛
استخدم مـتم؛
استخدم صـفوف؛

عرف قب: قـاعدة_بيانات(مـشغل_مايسكويل(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1, نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

عرف أسماء: مـصفوفة[نـص]({ نـص("سرمد"), نـص("سليمان"), نـص("عبد الله"), نـص("سالم") })؛
عرف عناوين: مـصفوفة[نـص]({ نـص("كندا"), نـص("سوريا"), نـص("السعودية"), نـص("السعودية") })؛

قب.نفذ("delete from roles")؛
قب.نفذ("delete from users")؛

عرف ع: صحيح؛

لكل ع = 0, ع < أسماء.هات_الطول(), ++ع {
    إذا !!قب.نفذ(إدخـال().{
        الجدول = نـص("users")؛
        الحقول = مـصفوفة[نـص]({ نـص("id"), نـص("name"), نـص("address") })؛
        البيانات = مـصفوفة[نـص]({ نـص("15") + ع, أسماء(ع), عناوين(ع)  })؛
    }) {
        نـظام.فشل(1, نـص("فشلت العملية: ") + قب.هات_آخر_خطأ())؛
    }
}

عرف ن: نـتيجة = قب.نفذ(إدخـال().{
    الجدول = نـص("roles")؛
    الحقول = مـصفوفة[نـص]({ نـص("id"), نـص("name"), نـص("userId") })؛
    البيانات = مـصفوفة[نـص]({ نـص("250"), نـص("admin"), نـص("150") })؛
})؛
إذا !!ن {
    نـظام.فشل(1, نـص("فشلت العملية: ") + ن.خطأ)؛
}

