اشمل "مـتم/نـص"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/سندات"؛
اشمل "مـتم/بـعدم"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/سـكويلايت.أسس" })؛
استخدم مـتم؛
استخدم صـفوف؛

@جدول["سيارات"، 1]
صنف سـيارة {
    عرف_أساسيات_الجدول[]؛

    @إلزامي
    @فهرس_رئيسي
    @عـدد_صحيح
    @حقل
    عرف المعرف: صحيح؛

    @مـحارف_مرنة["50"]
    @مبدئي["سيارتي"]
    @حقل
    عرف الاسم: نـص؛

    @عـدد_عائم
    @حقل["سعر_السيارة"]
    عرف السعر: عائم؛

    @ثـنائية
    @حقل
    عرف كهربائية: بـعدم[ثـنائي]؛
}

عرف قب: قـاعدة_بيانات(مـشغل_سكويلايت(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "بياناتي.db"؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1، نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

قب.مهيكل[سـيارة].واكب()؛

قب.من[سـيارة].احذف()؛

عرف ع: صحيح=1
لكل ع = 1، ع < 30، ع = ع+1 {
    عرف س: سـيارة؛
    س.المعرف = ع
    س.الاسم = نـص("سيارة ") + ع؛
    س.السعر = 200.0 * ع
    إذا ع > 5 س.كهربائية = ع > 15؛
    قب.احفظ[سـيارة](س)؛
}
طـرفية.اطبع("\ج")؛

دالة اطبع_حقولا(ح: مـصفوفة[سـندنا[سـيارة]]) {
    عرف ك: صحيح = 0؛
    لكل ك = 0، ك < ح.هات_الطول()، ك = ك+1 {
        طـرفية.اطبع(ح(ك).المعرف)؛
        طـرفية.اطبع("\t\t")؛
        طـرفية.اطبع(ح(ك).الاسم)؛
        طـرفية.اطبع("\t\t")؛
        طـرفية.اطبع(ح(ك).السعر)؛
        طـرفية.اطبع("\t\t")؛
        إذا ح(ك).كهربائية.أهو_عدم طـرفية.اطبع("غير معروف")
        وإلا إذا ح(ك).كهربائية طـرفية.اطبع("كهربائية")
        وإلا طـرفية.اطبع("تقليدية")؛
        طـرفية.اطبع("\ج")؛
    }
}

عرف د: مـصفوفة[سـندنا[سـيارة]]؛
د = قب.من[سـيارة].حيثما[(السعر < 1000 أو السعر > 5000) و المعرف > 2].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د(0).احذف()؛

د(1).الاسم = نـص("سيارة 4م")؛
د(1).السعر = 400؛
د(1).احفظ()؛

قب.من[سـيارة].حيثما[المعرف == 20].حدث[{ السعر=90، الاسم="BMW" }]؛

قب.من[سـيارة].حيثما[المعرف > 23 و المعرف <= 28].احذف()؛

د = قب.من[سـيارة].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[ليس (المعرف > 10)].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[!(المعرف في (1، 2، 5، 7))].اجلب()؛
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[المعرف في (1, 2, 4, 5, 7)].رتب[(-السعر, المعرف)].اجلب();
اطبع_حقولا(د)؛
طـرفية.اطبع("\ج")؛

د = قب.من[سـيارة].حيثما[المعرف في (1, 2, 4, 5, 7)].رتب[(-السعر, -المعرف)].اجلب();
اطبع_حقولا(د)؛

