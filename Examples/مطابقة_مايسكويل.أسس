اشمل "مـتم/نـص"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـتم/نـظام"؛
اشمل "مـتم/سندات"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، "صـفوف.أسس")؛
استخدم مـتم؛
استخدم صـفوف؛

@جدول["سيارات"]
صنف سـيارة {
    عرف_أساسيات_الجدول[]؛

    @إلزامي
    @فهرس_رئيسي
    @عـدد_صحيح
    @حقل
    عرف المعرف: صحيح؛

    @مـحارف_مرنة["50"]
    @مبدئي["سيارتي"]
    @حقل
    عرف الاسم: نـص؛

    @عـدد_عائم
    @حقل["سعر_السيارة"]
    عرف السعر: عائم؛
}

عرف قب: قـاعدة_بيانات(سـائق_مايسكويل(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1، نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

قب.مهيكل[سـيارة].أنشئ()؛

عرف ع: صحيح=1
لكل ع = 1، ع < 30، ع = ع+1 {
    عرف س: سـيارة؛
    س.المعرف = ع
    س.الاسم = نـص("سيارة ") + ع؛
    س.السعر = 200.0 * ع
    قب.احفظ[سـيارة](س)؛
}
طـرفية.اطبع("\ج")؛

عرف د: مـصفوفة[سـندنا[سـيارة]]؛
د = قب.من[سـيارة].حيثما[(السعر < 1000 أو السعر > 5000) و المعرف > 2].اجلب()؛

عرف ك: صحيح = 0؛
لكل ك = 0، ك < د.هات_الطول()، ك = ك+1 {
    طـرفية.اطبع(د(ك).المعرف)؛
    طـرفية.اطبع("  ")؛
    طـرفية.اطبع(د(ك).الاسم)؛
    طـرفية.اطبع("  ")؛
    طـرفية.اطبع(د(ك).السعر)؛
    طـرفية.اطبع("\ج")؛
}
طـرفية.اطبع("\ج")؛

د(0).احذف()؛

د(1).الاسم = نـص("سيارة 4 محدث")؛
د(1).السعر = 805؛
د(1).احفظ()؛

قب.من[سـيارة].حيثما[المعرف == 20].حدث[{ السعر=90، الاسم="BMW" }]؛

قب.من[سـيارة].حيثما[المعرف > 23 و المعرف <= 28].احذف()؛

د = قب.من[سـيارة].اجلب()؛

لكل ك=0، ك < د.هات_الطول()، ك=ك+1 {
    طـرفية.اطبع(د(ك).المعرف)؛
    طـرفية.اطبع("  ")؛
    طـرفية.اطبع(د(ك).الاسم)؛
    طـرفية.اطبع("  ")؛
    طـرفية.اطبع(د(ك).السعر)؛
    طـرفية.اطبع("\ج")؛
}

