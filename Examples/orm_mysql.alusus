import "../Rows";
use Srl;
use Rows;

@model["cars"]
class Car {
    define_model_essentials[];

    @notnull
    @pk
    @Integer
    @column
    def id: int;

    @VarChar["50"]
    @defult["My Car"]
    @column
    def name: String;

    @Float
    @column
    def price: float;
}

def db: Db(MysqlDriver(ConnectionParams().{
    dbName = "alusus";
    userName = "alusus";
    password = "alusus";
    host = "0.0.0.0";
}));
if !db.isConnected() {
    System.fail(1, String("Error connecting to DB: ") + db.getLastError());
}

db.schemaBuilder[Car].create();

def i: int=1
for i = 1, i < 30, i = i+1 {
    def c: Car;
    c.id = i
    c.name = String("Car ") + i;
    c.price = 200.0 * i
    db.save[Car](c);
}
Console.print("\n");

def d: Array[SrdRef[Car]];
d = db.from[Car].where[(price < 1000 or price > 5000) and id > 2].select();

def k: int=0;
for k = 0, k < d.getLength(), k = k+1 {
    Console.print(d(k).id);
    Console.print("  ");
    Console.print(d(k).name);
    Console.print("  ");
    Console.print(d(k).price);
    Console.print("\n");
}
Console.print("\n");

d(0).delete();

d(1).name = String("Car 4 updated");
d(1).price = 805;
d(1).save();

db.from[Car].where[id == 20].update[{ price=90, name="BMW" }];

db.from[Car].where[id > 23 and id <= 28].delete();

d = db.from[Car].select();

for k=0, k < d.getLength(), k=k+1 {
    Console.print(d(k).id);
    Console.print("  ");
    Console.print(d(k).name);
    Console.print("  ");
    Console.print(d(k).price);
    Console.print("\n");
}

