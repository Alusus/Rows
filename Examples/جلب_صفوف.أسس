اشمل "مـتم/نـظام"؛
اشمل "مـتم/نـص"؛
اشمل "مـتم/مـصفوفة"؛
اشمل "مـتم/طـرفية"؛
اشمل "مـحا"؛
مـحا.اشمل_ملف("Alusus/Rows"، { "صـفوف.أسس"، "مـشغلات/مـايسكويل.أسس" })؛
استخدم مـتم؛
استخدم صـفوف؛

عرف قب: قـاعدة_بيانات(مـشغل_مايسكويل(مـعطيات_الاتصال().{
    اسم_قاعدة_البيانات = "alusus"؛
    اسم_المستخدم = "alusus"؛
    كلمة_السر = "alusus"؛
    عنوان_الخادم = "0.0.0.0"؛
}))؛
إذا !قب.أمتصل() {
    نـظام.فشل(1, نـص("فشل الاتصال بقاعدة البيانات: ") + قب.هات_آخر_خطأ())؛
}

عرف بيانات: مـصفوفة[مـصفوفة[نـص]]؛
عرف نتيجة: نـتيجة[مـصفوفة[مـصفوفة[نـص]]]؛
نتيجة = قب.نفذ(جـلب().{
    الجدول = نـص("users")؛
})؛
إذا !!نتيجة {
    نـظام.فشل(1, نـص("فشلت العملية: ") + نتيجة.خطأ)؛
}
بيانات = نتيجة؛

عرف ص: صحيح = 0؛
عرف ع: صحيح = 0؛
لكل ص = 0, ص < بيانات.هات_الطول(), ص = ص + 1 {
    لكل ع = 0, ع < بيانات(ص).هات_الطول(), ع = ع + 1 {
        طـرفية.اطبع(بيانات(ص)(ع))؛
        طـرفية.اطبع("    ")؛
    }
    طـرفية.اطبع("\ج")؛
}

