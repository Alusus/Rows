@merge module Rows {
    class Db {
        def driver: SrdRef[Driver];
        def logging: Bool = true;

        handler this~init() {}
        handler this~init(d: SrdRef[Driver]) {
            this.driver = d;
        }

        handler this.init(d: SrdRef[Driver]) {
            this.driver = d;
        }

        handler this.isConnected(): Bool {
            return this.driver.obj~ptr != 0 && this.driver.isConnected();
        }

        handler this.getLastError(): String {
            if this.driver.obj~ptr == 0 return String("Not initialized")
            else return this.driver.getLastError();
        }

        handler this.exec(select: ref[Select]): Result[Array[Array[String]]] {
            return this._execSelect(select.generateSqlString(), "Select");
        }

        handler this.exec(update: ref[Update]): Result {
            return this._exec(update.generateSqlString(), "Update");
        }

        handler this.exec(insert: ref[Insert]): Result {
            return this._exec(insert.generateSqlString(), "Insert");
        }

        handler this.exec(delete: ref[Delete]): Result {
            return this._exec(delete.generateSqlString(), "Delete");
        }

        handler this.exec(createTable: ref[CreateTable]): Result {
            return this._exec(createTable.generateSqlString(this.driver), "Table creation");
        }

        handler this.execSelect(statement: CharsPtr, args: ...any): Result[Array[Array[String]]] {
            def sql: String;
            prepareStatement[sql, statement, args];
            return this._execSelect(sql, "Raw query");
        }

        handler this.exec(statement: CharsPtr, args: ...any): Result {
            def sql: String;
            prepareStatement[sql, statement, args];
            return this._exec(sql, "Raw query");
        }

        handler this._execSelect(sql: String, operationName: CharsPtr): Result[Array[Array[String]]] {
            if this.driver.obj~ptr == 0 {
                return Result[Array[Array[String]]].failure(String("Error: DB is not initialzied."));
            } else if !this.driver.isConnected() {
                return Result[Array[Array[String]]].failure(String("Error: DB is not connected."));
            } else {
                if this.logging {
                    Console.print("%s>> Executing SQL:%s\n%s\n", Console.Style.FG_GREEN, Console.Style.RESET, sql.buf);
                }
                if !this.driver.exec(sql, true) {
                    def err: String = this.driver.getLastError();
                    if this.logging {
                        Console.print("%s>> SQL Error:%s\n%s\n", Console.Style.FG_RED, Console.Style.RESET, err.buf);
                    }
                    return Result[Array[Array[String]]].failure(err);
                } else {
                    def numberOfFileds: int;
                    numberOfFileds = this.driver.getColumnCount();
                    def result: Array[Array[String]];
                    while this.driver.fetchNextRow() {
                        def row: Array[String];
                        def i: int;
                        for i = 0, i < numberOfFileds, i = i + 1 {
                            row.add(this.driver.getValue(i));
                        }
                        result.add(row);
                    }
                    return Result[Array[Array[String]]].success(result);
                }
            }
        }

        handler this._exec(sql: String, operationName: CharsPtr): Result {
            if this.logging {
                Console.print("%s>> Executing SQL:%s\n%s\n", Console.Style.FG_GREEN, Console.Style.RESET, sql.buf);
            }
            if this.driver.obj~ptr == 0 {
	            return Result.failure(String("Error: DB is not initialzied."));
            } else if !this.driver.isConnected() {
	            return Result.failure(String("Error: DB is not connected."));
            } else {
                if !this.driver.exec(sql, false) {
                    def err: String = this.driver.getLastError();
                    if this.logging {
                        Console.print("%s>> SQL Error:%s\n%s\n", Console.Style.FG_RED, Console.Style.RESET, err.buf);
                    }
                    return Result.failure(err);
                }
                return Result.success();
            }
        }

        handler [Model: type] this.from: Query[Model] {
            return Query[Model](this);
        }

        handler [Model: type] this.save(model: ref[Model]) {
            Query[Model](this).save(model);
        }

        handler [Model: type] this.schemaBuilder: SchemaBuilder[Model] {
            return SchemaBuilder[Model](this);
        }
    }
}

