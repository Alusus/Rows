/*
 * Copyright (C) 2026 Sarmad Abdullah
 *
 * This file is part of Alusus Rows library.
 *
 * This library is free software; you can redistribute it and/or
 * modify it under the terms of the GNU Lesser General Public
 * License as published by the Free Software Foundation; either
 * version 3 of the License, or (at your option) any later version.
 *
 * This library is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * Lesser General Public License for more details.
 *
 * You should have received a copy of the GNU Lesser General Public
 * License along with this library; if not, see <https://www.gnu.org/licenses/>.
 */

@merge module Rows {
    func createTableForModel[Model: type](db: ref[Db]): SrdRef[Error] {
        return db.exec(
            preprocess {
                if !Spp.astMgr.insertAst(generateTable(Model~ast).obj) {
                    System.fail(1, "Unexpected error in SchemaBuilder.create.\n");
                }
            }
        ).error;
    }

    func generateTable (classAst: ref[Core.Basic.TiObject]): TioSrdRef {
        use Core.Basic;
        def result: TioSrdRef;
        def columnResult: TioSrdRef;
        def columnResults: Spp.Ast.Block;
        def columnResultsContainer: DynContainerOf[columnResults];
        def pkResults: Core.Data.Ast.List;
        def pkResultsContainer: DynContainerOf[pkResults];
        def fkResults: Spp.Ast.Block;
        def fkResultsContainer: DynContainerOf[fkResults];

        def vars: Array[ref[Core.Basic.TiObject]] = getVars(classAst, 0);

        def i: int;
        def j: int;

        for i = 0, i < vars.getLength(), i += 1 {
            def modsKeywords: Array[String];
            def modsParams: Array[Array[String]];
            getVarModifiers(vars(i), modsKeywords, modsParams);

            def dataTypeName: String;
            for j = 0, j < modsKeywords.getLength(), j += 1 {
                if isDataType(modsKeywords(j)) != -1 {
                    dataTypeName = modsKeywords(j);
                    break;
                }
            }

            if dataTypeName == "" continue;

            def columnName: String = getColumnName(vars(i));

            if modsParams(j).getLength() == 0 {
                if !Spp.astMgr.buildAst(
                    (ast dataType = Rows.dataTypeName()),
                    AstTemplateMap().set(String("dataTypeName"), Ast.Identifier(dataTypeName)),
                    columnResult
                ) {
                    System.fail(1, "Unexpected error while processing data type in generateTable.\n");
                }
            } else {
                if !Spp.astMgr.buildAst(
                    (ast dataType = Rows.dataTypeName(size)),
                    AstTemplateMap()
                        .set(String("dataTypeName"), Ast.Identifier(dataTypeName))
                        .set(String("size"), Ast.IntegerLiteral(modsParams(j)(0))),
                    columnResult
                ) {
                    System.fail(1, "Unexpected error while processing data type in generateTable.\n");
                }
            }

            for j = 0, j < modsKeywords.getLength(), j += 1 {
                if modsKeywords(j) == "notNull" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            notNull = true;
                        },
                        AstTemplateMap().set(String("prevResult"), columnResult),
                        result
                    ) {
                        System.fail(1, "Unexpected error while processing not-null in generateTable.\n");
                    }
                    columnResult = result;
                } else if modsKeywords(j) == "unique" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            unique = true;
                        },
                        AstTemplateMap().set(String("prevResult"), columnResult),
                        result
                    ) {
                        System.fail(1, "Unexpected error while processing unique in generateTable.\n");
                    }
                    columnResult = result;
                } else if modsKeywords(j) == "primaryKey" {
                    if !Spp.astMgr.buildAst(
                        (ast Srl.String("{{varname}}")),
                        AstTemplateMap().set(String("varname"), Ast.Identifier(columnName)),
                        result
                    ) {
                        System.fail(1, "Unexpected error while processing primary-key in generateTable.\n");
                    }
                    pkResultsContainer.addElement(result);
                } else if modsKeywords(j) == "defult" {
                    if modsParams(j).getLength() == 0 {
                        Spp.buildMgr.raiseBuildNotice("ROWS1003", 1, vars(i));
                        continue;
                    }
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            default = Srl.String("{{param}}");
                        },
                        AstTemplateMap()
                            .set(String("prevResult"), columnResult)
                            .set(String("param"), Ast.Identifier(modsParams(j)(0))),
                        result
                    ) {
                        System.fail(1, "Unexpected error while processing default in generateTable.\n");
                    }
                    columnResult = result;
                } else if modsKeywords(j) == "check" {
                    if modsParams(j).getLength() == 0 {
                        Spp.buildMgr.raiseBuildNotice("ROWS1004", 1, vars(i));
                        continue;
                    }
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            _check = Srl.String("{{param}}");
                        },
                        AstTemplateMap()
                            .set(String("prevResult"), columnResult)
                            .set(String("param"), Ast.Identifier(modsParams(j)(0))),
                        result
                    ) {
                        System.fail(1, "Unexpected error while processing check in generateTable.\n");
                    }
                    columnResult = result;
                } else if modsKeywords(j) == "foreignKey" {
                    if modsParams(j).getLength() < 2 {
                        Spp.buildMgr.raiseBuildNotice("ROWS1005", 1, vars(i));
                        continue;
                    }
                    def fkResult: TioSrdRef;
                    if !Spp.astMgr.buildAst(
                        (ast Rows.ForeignKey(
                            Srl.String("{{col}}"),
                            Srl.String("{{refTbl}}"),
                            Srl.String("{{refCol}}")
                        )),
                        AstTemplateMap()
                            .set(String("col"), Ast.Identifier(columnName))
                            .set(String("refTbl"), Ast.Identifier(modsParams(j)(0)))
                            .set(String("refCol"), Ast.Identifier(modsParams(j)(1))),
                        fkResult
                    ) {
                        System.fail(1, "Unexpected error while processing foreign-key in generateTable.\n");
                    }
                    fkResultsContainer.addElement(fkResult);
                }
            }

            if !Spp.astMgr.buildAst(
                (ast set(Srl.String("{{varName}}"), Rows.Column().{ columnDesc })),
                AstTemplateMap()
                    .set(String("varName"), TiStr(columnName))
                    .set(String("columnDesc"), columnResult),
                result
            ) {
                System.fail(1, "Unexpected error while processing column definition in generateTable.\n");
            }
            columnResultsContainer.addElement(result);
        }

        if !Spp.astMgr.buildAst(
            (ast Rows.CreateTable().{
                name = Srl.String("{{tableName}}");
                notExists = true;
                columns = Srl.Map[Srl.String, Srl.SrdRef[Rows.Column]]().columnDefs;
                primaryKey = Srl.Array[Srl.String]({ pkColumns });
                foreignKeys = Srl.Array[Srl.SrdRef[Rows.ForeignKey]](fkDefs);
            }),
            AstTemplateMap()
                .set(String("tableName"), Ast.Identifier(getTableName(classAst)))
                .set(String("columnDefs"), columnResults)
                .set(String("pkColumns"), pkResults)
                .set(String("fkDefs"), fkResults),
            result
        ) {
            System.fail(1, "Unexpected error while processing table definition in generateTable.\n");
        }
        return result;
    }

    function isDataType (varType : String): int {
        @shared def dataTypeArray:Array[String]({
            String("Integer"),
            String("BigInteger"),
            String("SmallInteger"),
            String("TinyInteger"),
            String("Boolean"),
            String("Real"),
            String("Float"),
            String("Decimal"),
            String("Xml"),
            String("VarChar"),
            String("CharType"),
            String("Text"),
            String("Date"),
            String("DateTime"),
	        String("Uuid")
        });

        def si : int ;
        for si = 0, si < dataTypeArray.getLength(), ++si {
            if dataTypeArray(si) == varType {
                return si;
            }
        }
        return -1;
    }
}

