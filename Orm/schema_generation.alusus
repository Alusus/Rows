import "Spp";
import "Spp/Ast";

@merge module Rows {
    func generateTable (
        tableName: String, modsKeywords: Array[Array[String]], modsParams: Array[Array[Array[String]]],
        var: Array[String]
    ): TioSrdRef {
        use Core.Basic;
        def result: TioSrdRef;
        def columnResult: TioSrdRef;
        def columnResults: Spp.Ast.Block;
        def columnResultsContainer: DynContainerOf[columnResults];
        def pkResults: Core.Data.Ast.List;
        def pkResultsContainer: DynContainerOf[pkResults];
        def fkResults: Spp.Ast.Block;
        def fkResultsContainer: DynContainerOf[fkResults];

        def i: int;
        def j: int;

        // TODO: Handle error cases related to incorrect modifier params.

        for i = 0, i < var.getLength(), i += 1 {
            def dataTypeName: String;
            for j = 0, j < modsKeywords(i).getLength(), j += 1 {
                if isDataType(modsKeywords(i)(j)) != -1 {
                    dataTypeName = modsKeywords(i)(j);
                    break;
                }
            }

            if dataTypeName == "" continue;

            if modsParams(i)(j).getLength() == 0 {
                if !Spp.astMgr.buildAst(
                    (ast dataType = Rows.dataTypeName()),
                    Map[String, ref[TiObject]]().set(String("dataTypeName"), Core.Data.Ast.Identifier(dataTypeName)),
                    columnResult
                ) {
                    System.fail(1, "generateTable/build Data Type var : error\n");
                }
            } else {
                if !Spp.astMgr.buildAst(
                    (ast dataType = Rows.dataTypeName(size)),
                    Map[String, ref[TiObject]]()
                        .set(String("dataTypeName"), Core.Data.Ast.Identifier(dataTypeName))
                        .set(String("size"), Core.Data.Ast.IntegerLiteral(modsParams(i)(j)(0))),
                    columnResult
                ) {
                    System.fail(1, "generateTable/build Data Type var : error\n");
                }
            }

            for j = 0, j < modsKeywords(i).getLength(), j += 1 {
                if modsKeywords(i)(j) == "notNull" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            notNull = true;
                        },
                        Map[String, ref[TiObject]]().set(String("prevResult"), columnResult),
                        result
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    columnResult = result;
                } else if modsKeywords(i)(j) == "unique" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            unique = true;
                        },
                        Map[String, ref[TiObject]]().set(String("prevResult"), columnResult),
                        result
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    columnResult = result;
                } else if modsKeywords(i)(j) == "primaryKey" {
                    if !Spp.astMgr.buildAst(
                        (ast Srl.String("{{varname}}")),
                        Map[String, ref[TiObject]]().set(String("varname"), Core.Data.Ast.Identifier(var(i))),
                        result
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    pkResultsContainer.addElement(result);
                } else if modsKeywords(i)(j) == "defult" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            default = Srl.String("{{param}}");
                        },
                        Map[String, ref[TiObject]]()
                            .set(String("prevResult"), columnResult)
                            .set(String("param"), Core.Data.Ast.Identifier(modsParams(i)(j)(0))),
                        result
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    columnResult = result;
                } else if modsKeywords(i)(j) == "check" {
                    if !Spp.astMgr.buildAst(
                        ast {
                            prevResult;
                            _check = Srl.String("{{param}}");
                        },
                        Map[String, ref[TiObject]]()
                            .set(String("prevResult"), columnResult)
                            .set(String("param"), Core.Data.Ast.Identifier(modsParams(i)(j)(0))),
                        result
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    columnResult = result;
                } else if modsKeywords(i)(j) == "foreignKey" {
                    def fkResult: TioSrdRef;
                    if !Spp.astMgr.buildAst(
                        (ast Rows.ForeignKey(
                            Srl.String("{{col}}"),
                            Srl.String("{{refTbl}}"),
                            Srl.String("{{refCol}}")
                        )),
                        Map[String, ref[TiObject]]()
                            .set(String("col"), Core.Data.Ast.Identifier(var(i)))
                            .set(String("refTbl"), Core.Data.Ast.Identifier(modsParams(i)(j)(0)))
                            .set(String("refCol"), Core.Data.Ast.Identifier(modsParams(i)(j)(1))),
                        fkResult
                    ) {
                        System.fail(1, "generateTable/build Data Type var : error\n");
                    }
                    fkResultsContainer.addElement(fkResult);
                }
            }

            if !Spp.astMgr.buildAst(
                (ast set(Srl.String("{{varName}}"), Rows.Column().{ columnDesc })),
                Map[String, ref[TiObject]]()
                    .set(String("varName"), TiStr(var(i)))
                    .set(String("columnDesc"), columnResult),
                result
            ) {
                System.fail(1, "generateTable/build Data Type var : error\n");
            }
            columnResultsContainer.addElement(result);
        }

        def tblResult: TioSrdRef;
        if !Spp.astMgr.buildAst(
            ast {
                name = Srl.String("{{tableName}}");
                notExists = true;
                columns = Srl.Map[Srl.String, Srl.SrdRef[Rows.Column]]().columnDefs;
                primaryKey = Srl.Array[Srl.String]({ pkColumns });
                foreignKeys = Srl.Array[Srl.SrdRef[Rows.ForeignKey]](fkDefs);
            },
            Map[String, ref[TiObject]]()
                .set(String("tableName"), Core.Data.Ast.Identifier(tableName))
                .set(String("columnDefs"), columnResults)
                .set(String("pkColumns"), pkResults)
                .set(String("fkDefs"), fkResults),
            result
        ) {
            System.fail(1, "generateTable/build Data Type var : error\n");
        }
        tblResult = result;

        def fnResult: TioSrdRef;
        if !Spp.astMgr.buildAst(
            (ast if !this.db.exec(Rows.CreateTable().{ tableDef }) {
                Srl.System.fail(1, Srl.String("Table creation failed: ") + this.db.getLastError());
            }),
            Map[String, ref[TiObject]]().set(String("tableDef"), tblResult),
            fnResult
        ) {
            System.fail(1, "generateTable/build Data Type var : error\n");
        }
        return fnResult;
    }

    function isDataType (varType : String): int {
        @shared def dataTypeArray:Array[String]({
            String("Integer"),
            String("BigInteger"),
            String("SmallInteger"),
            String("TinyInteger"),
            String("Real"),
            String("Float"),
            String("Decimal"),
            String("Xml"),
            String("VarChar"),
            String("CharType"),
            String("Text"),
            String("Date"),
	    String("Uuid")
        });

        def si : int ;
        for si = 0, si < dataTypeArray.getLength(), ++si {
            if dataTypeArray(si) == varType {
                return si;
            }
        }
        return -1;
    }
}

