@merge module Rows {
    class Query [Model: type] {
        def db: ref[Db];
        def validationBuffer: Model;
        def where: String;
        def ordering: Array[String];

        handler this~init(db: ref[Db]) {
            this.db~no_deref = db;
        }

        handler this~init(value: ref[this_type]) {
            this.db~no_deref = value.db;
            this.where = value.where;
            this.ordering = value.ordering;
        }

        func getColumnNames (): Array[String] {
            @shared def names: Threading.ThreadLocal[Array[String]](closure (val: ref[Array[String]]) {
                val = Array[String]({
                    preprocess { generateColumnNames(Model~ast, "column") }
                });
            });
            return names.value;
        }

        func getColumnVarNames (): Array[String] {
            @shared def names: Threading.ThreadLocal[Array[String]](closure (val: ref[Array[String]]) {
                val = Array[String]({
                    preprocess { generateVarNames(Model~ast, "column") }
                });
            });
            return names.value;
        }

        handler this.getColumnNameForVar(varName: String): String {
            def map: Map[String, String];
            map.keys = getColumnVarNames();
            map.values = getColumnNames();
            return map(varName);
        }

        func getPrimaryColumnNames (): Array[String] {
            return Array[String]({
                preprocess { generateColumnNames(Model~ast, "primaryKey") }
            });
        }

        func getPrimaryColumnVarNames (): Array[String] {
            return Array[String]({
                preprocess { generateVarNames(Model~ast, "primaryKey") }
            });
        }

        func parseRecord (data: Array[Nullable[String]]): SrdRef[Model] {
            def model: SrdRef[Model];
            model.construct();
            preprocess {
                generateParsing(
                    getColumnVarNames(),
                    getVars(Model~ast, "column")
                );
            }
            return model;
        }

        func getRecordData [colummnNameGetter: function = getColumnVarNames] (model: ref[Model]): Array[Value] {
            def data: Array[Value];
            preprocess {
                generateGetData(colummnNameGetter());
            }
            return data;
        }

        handler this.getTableName(): String {
            return preprocess { generateTableName(Model~ast) };
        }

        handler this.setWhereForPrimaryKey(model: ref[Model]) {
            def columns: Array[String] = getPrimaryColumnNames();
            def data: Array[Value] = getRecordData[getPrimaryColumnVarNames](model);
            this.where = String("\"") + escapeString(columns(0)) + "\" = " + data(0).toString();
            def i: Int;
            for i = 1, i < columns.getLength(), ++i {
                this.where += " AND \"";
                this.where += escapeString(columns(i)) + "\" = " + data(i).toString();
            }
        }

        handler [exp: ast] this.order:ref[this_type] {
            preprocess {
                if !Spp.astMgr.insertAst(
                    generateOrder(exp~ast).obj
                ) {
                    System.fail(1, "Failed to insert order list.\n");
                }
            }
            return this;
        }

        @member macro where [this, condition] {
            this~use_in(_this) no_injection {
                _this.where = preprocess {
                    if !Spp.astMgr.insertAst(
                        Rows.generateWhere(ast condition).obj
                    ) {
                        Srl.System.fail(1, "Failed to insert condition.\n");
                    }
                }
            }
        }

        @member macro update [this, expression] {
            Possible[Int]()~use_in(_result) no_injection {
                this~use_in(_this) no_injection {
                    def data: Srl.Array[Rows.Value];
                    def columns: Srl.Array[Srl.String];
                    preprocess {
                        if !Spp.astMgr.insertAst(
                            Rows.generateUpdate(ast expression).obj
                        ) {
                            Srl.System.fail(1, "Failed to insert update statements.\n");
                        }
                    }
                    _result = _this.db.exec(Rows.Update()~use_in(update) no_injection {
                        update.table = _this.getTableName();
                        update.columns = columns;
                        update.data = data;
                        update._condition = _this.where;
                    });
                }
            }
        }

        handler this.select(): Possible[Array[SrdRef[Model]]] {
            def data: Array[Array[Nullable[String]]];
            this.db.exec(Select()~use_in(select) {
                table = this.getTableName();
                fields = getColumnNames();
                _condition = this.where;
                orderBy = this.ordering;
            })~use_in (res) no_injection {
                if !!res return Possible[Array[SrdRef[Model]]].failure(res.error);
                data = res;
            }
            def models: Array[SrdRef[Model]];
            def i: int =0;
            for i = 0, i < data.getLength(), ++i {
                def model: SrdRef[Model] = parseRecord(data(i));
                model.db~no_deref = this.db;
                models.add(model);
            }
            return Possible[Array[SrdRef[Model]]].success(models);
        }

        handler this.save(model: ref[Model]): Possible[Int] {
            if model.db~ptr == this.db~ptr {
                // Update an existing record.
                this.setWhereForPrimaryKey(model);
                return this.db.exec(Update()~use_in(update) no_injection {
                    update.table = this.getTableName();
                    update.columns = getColumnNames();
                    update.data = getRecordData(model);
                    update._condition = this.where;
                });
            } else {
                // Create a new record.
                def res: Possible[Int] = this.db.exec(Insert()~use_in(insert) no_injection {
                    insert.table = this.getTableName();
                    insert.columns = getColumnNames();
                    insert.data = getRecordData(model);
                });
                if res model.db~no_deref = this.db;
                return res;
            }
        }

        handler this.delete(model: ref[Model]): Possible[Int] {
            this.setWhereForPrimaryKey(model);
            def res: Possible[Int] = this.delete();
            if res model.db~ptr = 0;
            return res;
        }

        handler this.delete(): Possible[Int] {
            return this.db.exec(Delete()~use_in(delete) {
                delete.table = this.getTableName();
                delete._condition = this.where;
            });
        }
    }
}

