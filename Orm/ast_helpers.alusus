@merge module Rows {
    function generateElementNames (condition: ref[Core.Basic.TiObject], parent: ref[Core.Basic.TiObject]) {
        generateStrings(getElementNames(condition, parent));
    }

    function getElementNames (
        condition: ref[Core.Basic.TiObject],
        parent: ref[Core.Basic.TiObject]
    ): Array[String] {
        def elements: Array[ref[Core.Basic.TiObject]];
        elements = Spp.astMgr.findElements(
            condition,
            parent,
            Spp.SeekerFlags.SKIP_OWNERS | Spp.SeekerFlags.SKIP_USES | Spp.SeekerFlags.SKIP_CHILDREN
        );
        def result: Array[String];
        def i: Int;
        for i = 0, i < elements.getLength(), ++i
        {
            def node: ref[Core.Data.Node](castRef[elements(i), Core.Data.Node]);
            def d: ref[Core.Data.Ast.Definition](castRef[node.owner, Core.Data.Ast.Definition]);
            def binding: ref[Core.Basic.Binding](Core.Basic.getInterface[d, Core.Basic.Binding]);
            result.add(castRef[binding.getMember("name"), TiStr].value);
        }
        return result;
    }

    function generateElementTypes (condition: ref[Core.Basic.TiObject], parent: ref[Core.Basic.TiObject]) {
        generateStrings(getElementTypes(condition, parent));
    }

    function getElementTypes (
        condition: ref[Core.Basic.TiObject],
        parent: ref[Core.Basic.TiObject]
    ): Array[String] {
        def elements: Array[ref[Core.Basic.TiObject]];
        elements = Spp.astMgr.findElements(
            condition,
            parent,
            Spp.SeekerFlags.SKIP_OWNERS | Spp.SeekerFlags.SKIP_USES | Spp.SeekerFlags.SKIP_CHILDREN
        );
        def result: Array[String];
        def i: Int;
        for i = 0, i < elements.getLength(), ++i
        {
            // TODO: Trace the actual type rather than looking at the identifier.
            dynCastRef[elements(i), Core.Data.Ast.Identifier].{
                if this~ptr == 0 {
                    System.fail(1, "getElementTypes error: Invalid element type.");
                }
                result.add(this.value.value);
            }
        }
        return result;
    }

    function generateStrings (strs: Array[String]) {
        def i: Int;
        for i = 0, i < strs.getLength(), ++i {
            Spp.astMgr.insertAst(
                (ast Srl.String("{{str}}")),
                Srl.Map[Srl.String, ref[Core.Basic.TiObject]]().set(Srl.String("str"), TiStr(strs(i)))
            );
        }
    }

    function getElementModifiers (
        condition: ref[Core.Basic.TiObject],
        parent: ref[Core.Basic.TiObject],
        modsKeywords: ref[Array[Array[String]]],
        modsParams: ref[Array[Array[Array[String]]]]
    ) {
        def elements: Array[ref[Core.Basic.TiObject]];
        def i: int = 0;
        def j: int = 0;
        elements = Spp.astMgr.findElements(
            condition,
            parent,
            Spp.SeekerFlags.SKIP_OWNERS | Spp.SeekerFlags.SKIP_USES | Spp.SeekerFlags.SKIP_CHILDREN
        );
        modsKeywords = getElementsModifierKeywords(elements);

        for i = 0, i < modsKeywords.getLength(), ++i {
            def params: Array[Array[String]];
            for j = 0, j < modsKeywords(i).getLength(), ++j {
                params.add(getElementModifierParams(elements(i), modsKeywords(i)(j)));
            }
            modsParams.add(params);
        }
    }

    function getElementsModifierKeywords (elements: Array[ref[Core.Basic.TiObject]]): Array[Array[String]] {
        def modsKeywords: Array[Array[String]];
        def i: Int;
        for i = 0, i < elements.getLength(), ++i {
            modsKeywords.add(getElementModifierKeywords(elements(i)));
        }
        return modsKeywords;
    }

    function getElementModifierKeywords (element: ref[Core.Basic.TiObject]): Array[String] {
        def modifiers: ref[Core.Basic.Containing](Spp.astMgr.getModifiers(element));
        def i: Int;
        def strArray: Array[String] ;
        if modifiers~ptr != 0 {
            for i = 0, i < modifiers.getElementCount()~cast[Int], ++i {
                strArray.add(Spp.astMgr.getModifierKeyword(modifiers.getElement(i)));
            }
        }
        return strArray;
    }

    function getElementModifierParams (element: ref[Core.Basic.TiObject], modName: ptr[array[Char]]): Array[String] {
        def modifiers: ref[Core.Basic.Containing](Spp.astMgr.getModifiers(element));
        def modifier: ref[Core.Basic.TiObject](Spp.astMgr.findModifier(modifiers, modName));
        def params: Array[String];
        if modifier~ptr != 0 params = getModifierParams(modifier);
        return params;
    }

    function getModifierParams (modifier: ref[Core.Basic.TiObject]): Array[String] {
        def params: Srl.Array[Srl.String];
        Spp.astMgr.getModifierStringParams(modifier, params);
        return params;
    }
}

